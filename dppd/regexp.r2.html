<HTML><HEAD>
<TITLE>DPPD: regexp.r2</TITLE>
</HEAD>

<BODY>

<H2>The "<DFN>regexp.r2</DFN>" Benchmark</H2>

Part of the <A HREF="../dppd.html">DPPD Library</A>.


<H3>General Description</H3>

A program testing whether a string matches a regular expression (using difference lists). Much more naive (and smaller) than the program used by Mogensen/Bondorf for Logimix !
The regular expression for this benchmark is ((a+b)(c+d)(e+f)(g+h))*.

This benchmark
 contains no built-in's nor negations.

<H3>The benchmark program</H3>

<PRE>
generate(empty,T,T).

generate(char(X),[X|T],T).

generate(or(X,Y),H,T) :- generate(X,H,T).
generate(or(X,Y),H,T) :- generate(Y,H,T).

generate(cat(X,Y),H,T) :- generate(X,H,T1), generate(Y,T1,T).

generate(star(X),T,T).
generate(star(X),H,T) :- generate(X,H,T1), generate(star(X),T1,T).
</PRE>


<H3>The partial deduction query</H3>

<PRE>
 :- generate(star(cat(or(char(a),char(b)),cat(or(char(c),char(d)), 
	cat(or(char(e),char(f)),or(char(g),char(h)))))), S,[]).
</PRE>



<H3>The run-time queries</H3>

<PRE>
 :- generate(star(cat(or(char(a),char(b)),cat(or(char(c),char(d)), 
		cat(or(char(e),char(f)),or(char(g),char(h)))))),
		[a,d,e,h, b,c,f,g],[]).
 :- generate(star(cat(or(char(a),char(b)),cat(or(char(c),char(d)), 
		cat(or(char(e),char(f)),or(char(g),char(h)))))),
		[b,d,X,g, a,c,Y,h],[]).
 :- generate(star(cat(or(char(a),char(b)),cat(or(char(c),char(d)), 
		cat(or(char(e),char(f)),or(char(g),char(h)))))),
		[a,d,e,h, b,z,f,g, a,d,e,h, b,c,f,g, a,d,e,h, b,c,f,g],[]).
</PRE>



<H3>Example solution</H3>

The following can be obtained by the 
<A HREF="/~mal/systems/ecce.html">ECCE partial deduction system </A>.
It runs considerably faster than the original program (2.5 times
 actually) and correspond to a deterministic automaton.

<PRE>
 generate__1([]).
 generate__1(X1) :- generate_conj__2(X1).

 generate_conj__2([a|X1]) :- generate_conj__3(X1).
 generate_conj__2([b|X1]) :- generate_conj__3(X1).

 generate_conj__3([c|X1]) :- generate_conj__4(X1).
 generate_conj__3([d|X1]) :- generate_conj__4(X1).

 generate_conj__4([e|X1]) :- generate_conj__5(X1).
 generate_conj__4([f|X1]) :- generate_conj__5(X1).

 generate_conj__5([g|X1]) :- generate__1(X1).
 generate_conj__5([h|X1]) :- generate__1(X1).
</PRE>


<HR>
<!-- ================================================================= -->


<ADDRESS>
<A HREF="http://www.stups.uni-duesseldorf.de/w/Imprint">Imprint</A>
</ADDRESS>
