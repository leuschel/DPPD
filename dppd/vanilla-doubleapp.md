The "vanilla-doubleapp" Benchmark
---------------------------------

Part of the [DPPD Library](../dppd.html).

### General Description

This is a variation of the vanilla metainterpreter specialised for
double-append as object program. More details can be found in the paper:

1.  [M. Leuschel, S.-J. Craig, M. Bruynooghe, W. Vanhoof, Specialising
    Interpreters Using Offline Partial Deduction, in: Program
    Development in Computational Logic, Springer LNCS 3049, 2004, pp.
    340-375.](http://www.stups.uni-duesseldorf.de/w/Special:Publication/LeCrBrVa04_19)

### The benchmark program

Note: the program below also contains hints for the [BTA for Logen based
on
size-change-analysis](http://www.stups.uni-duesseldorf.de/w/Special:Publication/LeVi08_234).

        '$MEMOANN'(solve,1,[list_nv]).

        solve([]).
        solve([A|T]) :- solve_atom(A), solve(T).

        solve_atom(A) :- my_clause(A,B), solve(B).



        my_clause(doubleapp(X,Y,Z,R),[app(X,Y,I),app(I,Z,R)]).
        my_clause(app([],L,L),[]).
        my_clause(app([H|X],Y,[H|Z]),[app(X,Y,Z)]).
        my_clause(p,[p]).
        my_clause(solve2([]),[]).
        my_clause(solve2([A|T]), [solve_atom2(A), solve2(T)]).
        my_clause(solve_atom2(A), [my_clause2(A,B), solve2(B)]).
        my_clause(my_clause2(app([],L,L),[]),[]).
        my_clause(my_clause2(app([H|X],Y,[H|Z]),[app(X,Y,Z)]),[]).


        test2(R) :-
           solve_atom(solve_atom2(app([a,b,c],[d,e,f,g],R))).
        test1(R) :-
           solve_atom(app([a,b,c],[d,e,f,g],R)).

### The partial deduction query

     :- solve([doubleapp(X,Y,Z,R)]).

### The run-time queries

     :- solve([doubleapp([a,b,c,d,e,f,d],[a,b,c,d,e,f,d],[a,b,c,d,e,f,d],A)]).
     :- solve([doubleapp([a,b,c,d,e,f,d,e,g,h,i,l,m,n],[a,b,c,d,e,f,d,e,g,h,i,l,m,n],[a,b,c,d,e,f,d,e,g,h,i,l,m,n],A)]).

### Example solution

The following was obtained using
`ecce tests/vanilla_list.pl -pe "solve([doubleapp(X,Y,Z,R)])"`:

        /* Specialised program generated by ECCE 2.0 */
        /* PD Goal: A */
        /* Parameters: Abs:l InstCheck:v Msv:s NgSlv:g Part:e Prun:n Sel:t Whstl:f Raf:yesFar:yes Dce:yes Poly:y Dpu:yes ParAbs:yes Msvp:no Rrc:yes */
        /* Transformation time: 20 ms */
        /* Unfolding time: 20 ms */
        /* Post-Processing time: 20 ms */

        /* Specialised Predicates: 
        solve__1(A,B,C,D) :- solve([doubleapp(A,B,C,D)]).
        solve_atom_conj__2(A,B,C,D) :- solve_atom(app(A,B,E1)), my_clause(app(E1,C,D),F1), solve(F1).
        my_clause_conj__3(A,B,C) :- my_clause(app(A,B,C),D1), solve(D1).
        */

        solve([doubleapp(A,B,C,D)]) :- 
            solve__1(A,B,C,D).
        solve__1([],A,B,C) :- 
            my_clause_conj__3(A,B,C).
        solve__1([A|B],C,D,[A|E]) :- 
            solve_atom_conj__2(B,C,D,E).
        solve_atom_conj__2([],A,B,C) :- 
            my_clause_conj__3(A,B,C).
        solve_atom_conj__2([A|B],C,D,[A|E]) :- 
            solve_atom_conj__2(B,C,D,E).
        my_clause_conj__3([],A,A).
        my_clause_conj__3([A|B],C,[A|D]) :- 
            my_clause_conj__3(B,C,D).

------------------------------------------------------------------------

[Imprint](http://www.stups.uni-duesseldorf.de/w/Imprint)
