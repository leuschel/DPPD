<HTML><HEAD>
<TITLE>DPPD: liftsolve.db1</TITLE>
</HEAD>

<BODY>

<H2>The "<DFN>liftsolve.db1</DFN>" Benchmark</H2>

Part of the <A HREF="https://github.com/leuschel/DPPD">DPPD Library</A>.


<H3>General Description</H3>

Specialise the lifting meta-interpreter for the ground representation 
(adapted from a "non-executable" one by John Gallagher,
 similar to the InstanceDemo by Hill and Gallagher)
with a simple object program which can be fully unfolded.
Some details about this meta-interpreter can also be found
 in a 
<A HREF=http://www.cs.kuleuven.ac.be/cwis/research/dtai/publications/abstracts.95.html#leuschel.pepm95.abstract>PEPM'95 paper</A> by Michael Leuschel
 and Danny De Schreye.

<H3>The benchmark program</H3>

<PRE>
/* --------------------- */
/* solve(GrRules,NgGoal) */
/* --------------------- */
solve(GrRules,[]).
solve(GrRules,[NgH|NgT]) :-
	non_ground_member(term(clause,[NgH|NgBody]),GrRules),
	solve(GrRules,NgBody),
	solve(GrRules,NgT).

/* -------------------------------------- */
/* non_ground_member(NgExpr,GrListOfExpr) */
/* -------------------------------------- */
non_ground_member(NgX,[GrH|GrT]) :-
	make_non_ground(GrH,NgX).
non_ground_member(NgX,[GrH|GrT]) :-
	non_ground_member(NgX,GrT).


/* --------------------------------------------------- */
/* make_non_ground(GroundRepOfExpr,NonGroundRepOfExpr) */
/* --------------------------------------------------- */
make_non_ground(G,NG) :-
	mkng(G,NG,[],Sub).

mkng(var(N),X,[],[sub(N,X)]).
mkng(var(N),X,[sub(N,X)|T],[sub(N,X)|T]).
mkng(var(N),X,[sub(M,Y)|T],[sub(M,Y)|T1]) :-
	N \== M,
	mkng(var(N),X,T,T1).
mkng(term(F,Args),term(F,IArgs),InSub,OutSub) :-
	l_mkng(Args,IArgs,InSub,OutSub).

l_mkng([],[],Sub,Sub).
l_mkng([H|T],[IH|IT],InSub,OutSub) :-
	mkng(H,IH,InSub,IntSub),
	l_mkng(T,IT,IntSub,OutSub).


member(X,[X|T]).
member(X,[Y|T]) :-
	member(X,T).

append([],L,L).
append([H|T],M,[H|T2]) :-
	append(T,M,T2).
</PRE>


<H3>The partial deduction query</H3>

<PRE>
 :- solve([
	  term(clause,[term(father,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(male,[var(x)])]),
	  term(clause,[term(mother,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(female,[var(x)])]),
	  term(clause,[term(parent,[term(a,[]),term(b,[])])]),
	  term(clause,[term(male,[term(a,[])])])],
	[term(father,[X,Y])]).
</PRE>



<H3>The run-time queries</H3>

<PRE>
 :- solve([
	  term(clause,[term(father,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(male,[var(x)])]),
	  term(clause,[term(mother,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(female,[var(x)])]),
	  term(clause,[term(parent,[term(a,[]),term(b,[])])]),
	  term(clause,[term(male,[term(a,[])])])],
	[term(father,[X,Y])]).
 :- solve([
	  term(clause,[term(father,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(male,[var(x)])]),
	  term(clause,[term(mother,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(female,[var(x)])]),
	  term(clause,[term(parent,[term(a,[]),term(b,[])])]),
	  term(clause,[term(male,[term(a,[])])])],
	[term(father,[term(a,[]),Y])]).
 :- solve([
	  term(clause,[term(father,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(male,[var(x)])]),
	  term(clause,[term(mother,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(female,[var(x)])]),
	  term(clause,[term(parent,[term(a,[]),term(b,[])])]),
	  term(clause,[term(male,[term(a,[])])])],
	[term(father,[term(a,[]),term(b,[])])]).
 :- solve([
	  term(clause,[term(father,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(male,[var(x)])]),
	  term(clause,[term(mother,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(female,[var(x)])]),
	  term(clause,[term(parent,[term(a,[]),term(b,[])])]),
	  term(clause,[term(male,[term(a,[])])])],
	[term(father,[term(a,[]),term(c,[])])]).
 :- solve([
	  term(clause,[term(father,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(male,[var(x)])]),
	  term(clause,[term(mother,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(female,[var(x)])]),
	  term(clause,[term(parent,[term(a,[]),term(b,[])])]),
	  term(clause,[term(male,[term(a,[])])])],
	[term(father,[term(c,[]),Y])]).
 :- solve([
	  term(clause,[term(father,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(male,[var(x)])]),
	  term(clause,[term(mother,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(female,[var(x)])]),
	  term(clause,[term(parent,[term(a,[]),term(b,[])])]),
	  term(clause,[term(male,[term(a,[])])])],
	[term(father,[X,term(b,[])])]).
 :- solve([
	  term(clause,[term(father,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(male,[var(x)])]),
	  term(clause,[term(mother,[var(x),var(y)]),
		term(parent,[var(x),var(y)]),term(female,[var(x)])]),
	  term(clause,[term(parent,[term(a,[]),term(b,[])])]),
	  term(clause,[term(male,[term(a,[])])])],
	[term(father,[X,term(f,[term(c,[])])])]).
</PRE>



<H3>Example solution</H3>

This benchmark can be fully unfolded.
With the 
<A HREF="/~mal/systems/ecce.html">ECCE partial deduction system </A> one can obtain the following:

<PRE>
 solve__1(term(a,[]),term(b,[])).
</PRE>


<HR>
<!-- ================================================================= -->


<ADDRESS>
<A HREF="http://www.stups.uni-duesseldorf.de/w/Imprint">Imprint</A>
</ADDRESS>
